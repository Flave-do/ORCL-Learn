#   1.存储过程：是数据库中的一个对象

####      关键字：procedure

##     1.1.存储过程的创建：

```plsql
 create [or replace] procedure 存储过程名[(参数列表)]
      is|as
      [变量，游标等等声明]
      begin
        存储过程中需要执行的代码
      [exception
         异常处理]
     end [存储过程名];   
```

##### 注意:存储过程在创建的时候并不会执行其中的代码，只是对代码进行语法语义检查以及编译然后保存到数据库中，如果要执行则必须要调用。 

### 1.2.存储过程的调用：

#####   方式一：使用PL/SQL块调用

#####   方式二：使用call调用

​        call  存储过程名([参数值]);

#####         注意：不管存储过程是否有参数，则存储过程名后面必须要有括号。



##### 参数传递：

​    形参：形式参数，是在程序定义的时候设置的参数，一般没有具体的值，但是也可以设置默认值。

​    实参：实际传入的参数，也就是在调用存储过程的时候实际传入的具体的值叫做实参。

   			 一般情况下，实参的数量必须要和形参的数量保持一致（除非形参有默认值）。

##### 传参的方式：

#####       1.2.1.按照位置进行传参：实参的位置要和形参的位置保持一致。

#####       1.2.2.按照形参名称传参：实参需要通过形参的名称来指定。

​                 如果有参数里面有一些是有默认值不需要传参，则需要使用按照名称传参。

​                 语法：参数名=>参数值

#####       1.2.3.混合传参：按照位置和名称混合进行传参

​                 如果第一个参数就是按照名称传参，则后面就必须要使用名称。



### 1.3.存储过程的参数模式：

   存储过程中有3中模式的参数：

#####    1.3.1.in模式参数：

​		又叫做输入参数，输入参数的值只能从外面传入，在存储过程内部，不能对输入参数的值进行重复赋值。

​              参数语法：参数名 [in] 数据类型

​              注意：一般可以将in关键字进行省略，默认为输入参数。

​      输入参数是可以设置默认值：

​             参数名 [in] 数据类型 default 默认值

​             注意：如果输入参数设置了默认值，则在传参的时候可以不用设置实参，当然也可以设置。

#####    1.3.2.out模式参数：

​		又叫做输出参数，输出参数的功能就相当于函数中的return，但是存储过程可以输出多个值。

​                输出参数的值，只能从存储过程内部被赋值，然后传出到存储过程外面。

​              参数语法：参数名   out  数据类型

​              注意：在调用有输出参数的存储过程的时候必须要定义变量来接受输出的数据。



#####    	1.3.3.in out模式参数：

​		输入输出参数，具备了输入参数和输出参数的所有特性，既可以将数据传入到存储过程中，又可以在存储过程中被重新赋值然后输出。

​           参数语法：参数名 in out 数据类型；


​    

####  注意：out模式和in out模式不能设置默认值。



##### 删除存储过程：

######    drop  procedure 存储过程名；



### 1.4.存储过程和函数的区别：

######     1.定义的关键字不一样。

######     2.函数返回数据必须要使用return关键字，但是存储过程返回数据必须要使用out参数

######     3.函数只能返回一个数据，但是存储过程可以返回多个数据。

######     4.调用方式不一样，函数可以使用select进行调用，但是存储过程不能使用select调用。

######     5.函数的参数只有一种参数模式：in模式，但是存储过程有3种参数模式。



### --编写一个存储过程，向部门表中添加数据（无参数）

```plsql
SELECT * FROM dept;

CREATE  OR REPLACE PROCEDURE pro_insert_dept
IS
error_msg VARCHAR2(100);
BEGIN
  --添加数据
  INSERT INTO dept values(61,'CCC','QWEQW');
  --提交事务
  COMMIT;
 --异常处理：
 EXCEPTION
   WHEN OTHERS THEN
     error_msg:=SQLERRM;
     dbms_output.put_line(error_msg);
 END pro_insert_dept;

--调用存储过程
BEGIN
  pro_insert_dept;
END;

--调用存储过程
  CALL pro_insert_dept();
```



### --编写一个存储过程，向部门表中添加数据（有参数）

```plsql
CREATE  OR REPLACE PROCEDURE pro_insert_dept(d_no in NUMBER,d_name VARCHAR2 DEFAULT '市场部',d_loc VARCHAR2)
IS
error_msg VARCHAR2(100);
BEGIN
  --d_no:=78;

  --添加数据
  INSERT INTO dept values(d_no,d_name,d_loc);
  --提交事务
  COMMIT;
 --异常处理：
 EXCEPTION
   WHEN OTHERS THEN
     error_msg:=SQLERRM;
     dbms_output.put_line(error_msg);
 END pro_insert_dept;


--调用
CALL pro_insert_dept(71,d_loc=>'杭州');

--调用存储过程（按照位置进行传参）
CALL pro_insert_dept(68,'ffff','adsfsd');
--调用存储过程（按照名称进行传参）
CALL pro_insert_dept(d_no=>64,d_name => 'mmm',d_loc => 'nnn');
--混合传参
CALL pro_insert_dept(65,d_loc=> 'mmm',d_name => 'nnn');
```



### --编写一个存储过程，返回一个指定部门的平均工资

```plsql
CREATE OR REPLACE  PROCEDURE pro_avg_sal(d_no IN NUMBER DEFAULT NULL,avg_sal OUT NUMBER)
IS
error_msg VARCHAR2(100);
BEGIN
  IF d_no IS NULL THEN
    SELECT AVG(sal) INTO avg_sal FROM emp;
  ELSE
    SELECT AVG(sal) INTO avg_sal FROM emp WHERE deptno=d_no;
  END IF;
EXCEPTION
  WHEN OTHERS THEN
     error_msg:=SQLERRM;
     dbms_output.put_line(error_msg);
END pro_avg_sal;

--调用存储过程
DECLARE
 avg_sal1 NUMBER;
 BEGIN
  pro_avg_sal(30,avg_sal1);
  dbms_output.put_line(avg_sal1);
 END;
```



### --编写一个存储过程，查询指定员工编号的姓名和工资并返回

```plsql
CREATE OR REPLACE PROCEDURE pro_select_emp(e_no IN NUMBER,e_name OUT emp.ename%TYPE,e_sal OUT emp.sal%TYPE)
IS
  error_msg VARCHAR2(100);
BEGIN
  SELECT ename,sal INTO e_name ,e_sal  FROM emp WHERE empno=e_no;
EXCEPTION
  WHEN OTHERS THEN
     error_msg:=SQLERRM;
     dbms_output.put_line(error_msg);
END pro_select_emp;

--调用：
DECLARE
 ename emp.ename%TYPE;
 sal emp.sal%TYPE;
 BEGIN
    pro_select_emp(7369,ename,sal);
    dbms_output.put_line(ename||','||sal);
 END;
```





### --编写一个存储过程，输入一个数字和一个boolean类型的值，如果boolean为true则计算该数字的平方根，

 --否则计算该数字的平方，然后将结果重新赋值该变量进行输出。
--in out 模式

DROP  PROCEDURE pro_test;

```plsql
CREATE OR REPLACE PROCEDURE pro_test(n IN OUT NUMBER,boo BOOLEAN)
IS
BEGIN
  IF boo THEN
    n:=SQRT(n);
  ELSE
    n:=POWER(n,2);
  END IF;
END;


--调用
DECLARE
 n1 NUMBER;
 BEGIN
   n1:=9;
   pro_test(n1,TRUE);
   dbms_output.put_line(n1);
 END;





CREATE OR REPLACE PROCEDURE pro_test(n NUMBER,boo BOOLEAN,res OUT NUMBER)
IS
BEGIN
  IF boo THEN
    res:=SQRT(n);
  ELSE
    res:=POWER(n,2);
  END IF;
END;

--调用
DECLARE
 res1 NUMBER;
 BEGIN
   pro_test(9,FALSE,res1);
   dbms_output.put_line(res1);
 END;
```







##   2.增量和全量导数据

  merge into:

​		添加更新，如果目标表中存在该条数据，则进行修改，否则就进行添加

  		语法：

```plsql
merge into 目标表 using 原表 on (匹配条件)
	when matched then
	--当匹配到的时候需要执行的内容：只能做update操作
	update set 目标表.字段=原表.字段,......
	when not matched then
    --当匹配不成功的时候执行的内容：只能进行insert操作
    insert [(列名,列名,...)] values(值，值，值，....);
```



####  面试时候：

### 2.1.编写存储过程来实现增量和全量导数据

#### 2.1.1全量抽数：将源表中的所有的数据全部抽取到目标表中。

​       使用场景：如果表的数据量比较小，或者表中的数据变化很小基本这时候可以使用全量导数据。

​                例如：维度表中地区维度，时间维度等等。

##### 批量添加数据：

```plsql
   语法：insert into 表名 select 语句;
```



#### 过程：

##### 	2.1.1.1.清除目标表中的所有的数据：

```plsql
truncate table  目标表；（注意：在存储过程中必须要使用动态sql才能执行该语句）
```

##### 	2.1.1.2.将源表中的所有的数据批量导入到目标表中。

```plsql
     insert into 表名 select 语句;
```



#### 2.1.2.增量抽数：将源表中发生变化的数据抽取到目标表中。

​      使用场景：如果表中的数据量非常大，并且数据变化速度非常快，则比较适合使用增量到入数据。

​               例如：订单事实表等等，评论事实表等等。

​      前提条件：源表中需要有一个时间字段，来代表数据是否是最新的数据。

  

#### 过程：

#####     2.1.2.1.将目标表中指定时间的数据先进行删除。

​      delete删除

#####     2.1.2.2.根据传入的时间来从原表中查询出来发生变化的数据，然后将数据同步到目标表中。

​      merge into语句。

   

### --创建一张日志表，用来记录日志信息

```plsql
CREATE TABLE  table_log(
    start_time DATE,
    end_time DATE,
    pro_name VARCHAR2(50),
    sql_code VARCHAR2(20),
    sql_error VARCHAR2(255),
    status VARCHAR2(10)
);
```



### --编写一个存储过程实现全量抽数************************************************。

```plsql
CREATE OR REPLACE PROCEDURE full_load_data 
IS
--定义变量来记录错误信息
sql_error VARCHAR2(255);
sql_code VARCHAR2(50);
start_time DATE;
END_time DATE;
BEGIN
  --记录开始时间
  start_time:=SYSDATE;
  --清除目标表中原来的数据
  EXECUTE IMMEDIATE 'TRUNCATE TABLE full_user_t';
  --批量加载数据
  INSERT INTO full_user_t 
  SELECT 
  ID,
  NAME,
  pwd,
  u_date
  FROM user_t;
  --记录结束时间
  end_time:=SYSDATE;
  --记录日志信息
  INSERT INTO table_log VALUES(start_time,end_time,'full_load_data',sql_code,sql_error,'成功！');
  --提交事务
  COMMIT;
EXCEPTION
  WHEN OTHERS THEN
    --记录结束时间
    end_time:=SYSDATE;
    --记录日志
    sql_code:=SQLCODE;
    sql_error:=SQLERRM;
    INSERT INTO table_log VALUES(start_time,end_time,'full_load_data',sql_code,sql_error,'失败！');
    COMMIT;
END full_load_data;    

--调用存储过程：
CALL full_load_data();
```



### --编写存储过程，通过时间来进行增量抽取数据*******************************************

```plsql
CREATE  OR REPLACE PROCEDURE increment_load_data(load_date DATE)
IS
--定义变量来记录错误信息
sql_error VARCHAR2(255);
sql_code VARCHAR2(50);
start_time DATE;
END_time DATE;
BEGIN
  start_time:=SYSDATE;
  --将目标表中指定时间的数据进行删除(将要导入的数据先提前删除。)
  DELETE FROM INCREMENT_user_t WHERE u_date>load_date;
  --从原表中查询数据，然后将数据导入到目标表中
  --INSERT INTO increment_user_t
  --SELECT * FROM user_t WHERE u_date>load_date;
  --merge into：如果存在则修改，不存在则更新
  MERGE INTO increment_user_t t1 USING (SELECT * FROM user_t WHERE u_date>load_date) t2 ON (t1.id=t2.id)
  WHEN MATCHED THEN
    --更新
    UPDATE SET t1.name=t2.name,t1.pwd=t2.pwd,t1.u_date=t2.u_date
  WHEN NOT MATCHED THEN
    --插入
    INSERT VALUES(t2.id,t2.name,t2.pwd,t2.u_date);
  --结束：
    --记录结束时间
  end_time:=SYSDATE;
  --记录日志信息
  INSERT INTO table_log VALUES(start_time,end_time,'increment_load_data',sql_code,sql_error,'成功！');
  --提交事务
  COMMIT;
EXCEPTION
  WHEN OTHERS THEN
    --记录结束时间
    end_time:=SYSDATE;
    --记录日志
    sql_code:=SQLCODE;
    sql_error:=SQLERRM;
    INSERT INTO table_log VALUES(start_time,end_time,'increment_load_data',sql_code,sql_error,'失败！');
    COMMIT;
END increment_load_data;    


--调用存储过程
CALL increment_load_data(to_date('2022-10-24 15:30:00','yyyy-mm-dd HH24:mi:ss'));
```



### 2.2.编写存储过程来实现拉链表---用来保存历史记录。

​      拉链表是数据仓库中用来保存数据的一种方式，主要是用来解决数据仓库中的一个概念：缓慢变化维度。

​      拉链表是可以用来保存记录的历史信息，也就是从一个事务最开始的状态到当前状态的所有的变化情况。

#####       使用场景：

​			1.有一些表的数据量比较大，例如：用户表有10亿条数据，并且列很多，这时候如果每一次都要对
​      用户的数据进行备份，这样就会导致磁盘占用空间会很大。这时候可以使用拉链表。

​             2.如果一个表中的数据会发生修改，但是要保存他的历史信息，则可以使用拉链表。
​                

   拉链表：

```plsql
 业务表 
 id   name   pwd         修改时间
 1   小明    888888       2022-10-25
 2   小白    123456       2022-10-23
 3   小蓝    123456       2022-10-23
 4   小黑    123456       2022-10-25
```

  

拉链表

```plsql
 id   name   pwd         start_time            end_time 
 1   小明    123456       2022-10-24           2022-10-25
 2   小白    123456       2022-10-24           9999-12-31
 3   小蓝    123456       2022-10-24           9999-12-31
 1   小明    777777       2022-10-25           2022-10-26
 1   小明    888888       2022-10-26           9999-12-31
 4   小黑    123456       2022-10-26           9999-12-31
```



### --编写一个存储过程来实现拉链表****************************************************

```plsql
SELECT * FROM user_t;
CREATE TABLE lalian_user_t (
    ID NUMBER,
    NAME VARCHAR2(20),
    pwd VARCHAR2(20),
    u_date DATE,
    start_date DATE,
    end_date DATE DEFAULT to_date('9999-12-31','yyyy-mm-dd')); 
SELECT * FROM lalian_user_t;


CREATE OR REPLACE PROCEDURE lalian_load_data(load_date DATE)
IS
--定义变量来记录错误信息
sql_error VARCHAR2(255);
sql_code VARCHAR2(50);
start_time DATE;
END_time DATE;

--定义游标来查询变化的数据
CURSOR cur_increment IS
SELECT * FROM user_t WHERE u_date>load_date;

--定义游标来获取目标表中的数据
CURSOR cur_target IS
SELECT * FROM lalian_user_t WHERE end_date=to_date('9999-12-31','yyyy-mm-dd');

BEGIN
  --使用merge into进行数据的匹配，然后进行闭链操作
  --MERGE INTO lalian_user_t t1 USING (SELECT * FROM user_t WHERE u_date>load_date) t2 ON (t1.id=t2.id AND t1.end_date=to_date('9999-12-31','yyyy-mm-dd'))
  --WHEN MATCHED THEN
    --进行闭链：修改失效时间为当前的时间
   -- UPDATE SET t1.end_date=SYSDATE;
  FOR row_src IN cur_increment LOOP
    FOR row_target IN cur_target LOOP
      IF row_src.id=row_target.id THEN
        UPDATE lalian_user_t SET end_date=SYSDATE WHERE ID=row_src.id AND end_date=to_date('9999-12-31','yyyy-mm-dd');
        EXIT;
       END IF;
     END LOOP;
   END LOOP;
  --将新增和修改的数据进行添加
  INSERT INTO lalian_user_t 
  SELECT 
  ID,
  NAME,
  pwd,
  u_date,
  SYSDATE,
  to_date('9999-12-31','yyyy-mm-dd')
  FROM user_t WHERE u_date>load_date;
--结束：
    --记录结束时间
  end_time:=SYSDATE;
  --记录日志信息
  INSERT INTO table_log VALUES(start_time,end_time,'lalian_load_data',sql_code,sql_error,'成功！');
  --提交事务
  COMMIT;
  
EXCEPTION
  WHEN OTHERS THEN
    --记录结束时间
    end_time:=SYSDATE;
    --记录日志
    sql_code:=SQLCODE;
    sql_error:=SQLERRM;
    INSERT INTO table_log VALUES(start_time,end_time,'lalian_load_data',sql_code,sql_error,'失败！');
    COMMIT;
END lalian_load_data;
```

   

```plsql
CALL lalian_load_data(to_date('2022-10-24 16:55:00','yyyy-mm-dd HH24:mi:ss'));
```





###### --脏代码

```plsql
SELECT * FROM lalian_user_t;
SELECT * FROM user_t;
SELECT * FROM table_log;
INSERT INTO user_t(ID,NAME,pwd) VALUES(11,'大黄','123456');

UPDATE user_t SET NAME='大白',u_date=SYSDATE WHERE ID=2;

INSERT INTO user_t(ID,NAME,pwd) VALUES(10,'小紫','123456');
UPDATE user_t SET NAME='小拜拜',u_date=SYSDATE WHERE ID=2;

CREATE TABLE increment_user_t AS SELECT * FROM user_t;
SELECT * FROM INCREMENT_user_t;

SELECT * FROM user_t;
SELECT * FROM full_user_t;

CREATE TABLE full_user_t AS SELECT * FROM user_t WHERE 1=2;
```


