# 1.游标：cursor

   在SQL语句执行的时候，系统会自动分配一块内存空间来保存该sql语句执行所影响的数据，那么这一块空间就可以看做是

##    游标。

##    游标的分类：

##      	1.1.显式游标：

​        是由用户自己定义的游标，叫做显式游标。

​        显示游标主要是用来操作select语句执行的结果集。

​       游标操作步骤：

#####           1.1.1.定义游标：(游标定义必须要写在PL/SQL块的声明部分)

​            语法：cursor 游标名[(形参列表)] is

​                   select语句； 

#####           1.1.2.打开游标：(游标的打开操作要写在PL/SQL块的执行部分)

​            语法：open 游标名[(参数值)];

​			注意：1.一个已经被打开的游标不能重复打开，否则报错。

​						2.如果游标有参数则必须要传参数值。
​        

##### 		1.1.3.读取游标：

​    	语法：fetch 游标名 into 变量名，变量名,....;

##### 		1.1.4.关闭游标：

   	 语法：close  游标名;

​		注意：如果关闭一个没有打开的游标也会报错。



##### --编写一个PL/SQL块，查询所有的员工的信息，并打印

```plsql
DECLARE
 --定义一个显式游标
 CURSOR cur_emp IS
  SELECT * FROM emp;
 --定义一个变量
 row_emp emp%ROWTYPE;
 
BEGIN
  --打开游标
  OPEN cur_emp;
  --读取游标（使用loop循环）
  LOOP
    --读取游标
    FETCH cur_emp INTO row_emp;
    --退出循环
    EXIT WHEN cur_emp%NOTFOUND;
     dbms_output.put_line(row_emp.ename||','||row_emp.job||','||row_emp.sal);
    END LOOP;
   --关闭游标
   CLOSE cur_emp;
END;
```



##### --使用while循环

###### --编写一个PL/SQL块，查询指定部门的员工的姓名和工资

```plsql
DECLARE
 --定义游标
 CURSOR cur_emp(v_no NUMBER) IS
 SELECT  ename,sal FROM emp WHERE deptno=v_no;
 --定义变量
 v_name emp.ename%TYPE;
 v_sal emp.sal%TYPE;
 BEGIN
   IF cur_emp%ISOPEN THEN
     dbms_output.put_line('游标已经打开');
   ELSE
     dbms_output.put_line('游标没有打开');
   END IF;
   --打开游标
   OPEN cur_emp(20);

   --读取一次游标
   FETCH cur_emp INTO v_name,v_sal;
   --判断是否循环
   WHILE cur_emp%FOUND LOOP
     dbms_output.put_line(v_name||','||v_sal);
     --再次读取游标
     FETCH cur_emp INTO v_name,v_sal;
   END LOOP;
   --关闭游标
   CLOSE cur_emp;
 END;
```



##### --使用for循环操作游标

###### --编写一个PL/SQL块，查询每一个部门中工资最高的前两名员工的信息。

```plsql
DECLARE
--定义游标
CURSOR cur_top2 IS
SELECT * FROM (
SELECT e.*,row_number() OVER(PARTITION BY deptno ORDER BY sal DESC) rn FROM emp e) WHERE rn<=2;
BEGIN
  --使用for循环
  FOR row_emp IN cur_top2 LOOP
    dbms_output.put_line(row_emp.ename||','||row_emp.sal||','||row_emp.rn);
  END LOOP;
END;
```

#### 注意：

​	1.如果使用for循环来操作显示游标，则不需要自己打开，读取和关闭游标，系统会自动完成上述操作。

​	2.for循环的循环变量其实是一个记录类型，里面包含游标中的每一行数据中的所有列的信息，自动保存了游标读取出来的数据。

## 	1.2.隐式游标：

   主要是用来操作DML（insert，update,delete等等）语句，当然也可以用来操作查询语句。

   在Oracle中，每执行一条sql语句，系统都会为该语句来创建一个游标，这一类游标叫做隐式游标。

   所有的隐式游标统一名称为：sql



##### --使用隐式游标

###### --将20号部门的员工的工资上调10%，并且输出上调人数。

```plsql
BEGIN
  UPDATE emp SET sal=sal*1.1 WHERE deptno=20;
  --打印上调人数
  dbms_output.put_line('上调的人数为：'||sql%ROWCOUNT);
END;
  
SELECT * FROM emp WHERE deptno=20;
```



##### --使用for循环操作隐式游标结果集

###### --查询员工表中所有的数据

```plsql
BEGIN
  FOR row_emp IN (SELECT * FROM emp) LOOP
   dbms_output.put_line(row_emp.ename||','||row_emp.job||','||row_emp.sal);
  END LOOP;
END;  
```



#### 注意：隐式游标中保存的数据永远是最近执行的SQL语句的数据。

 

## 	1.3.引用游标（REF游标）:

​	在一个存储过程如果要返回一个结果集则需要使用引用游标。



## 	1.4.游标属性：

#####    	1.4.1.%notfound:

​			返回布尔类型的数据，如果游标没有读到数据则返回true,否则返回false.

#####   	 1.4.2.%fount:

​			返回布尔类型的数据，如果游标没有读到数据则返回false,否则返回true.

#####    	1.4.3.%rowcount:

​			返回一个数字，主要是用来返回游标所执行的sql语句影响的行数。

#####    	1.4.4.%isopen:

​			返回一个布尔类型的数据，用来判断游标是否被打开，如果打开返回true,否则返回false.

  

#   2.异常：

##     2.1、 异常处理的语法：

```plsql
     exception
       when 异常名称 then
         异常处理的代码;
       when 异常名称 then
          异常处理的代码;
       ....
       when others then
         异常处理的代码;
```

​       

#### 注意：others可以捕获所有的异常，所以如果有多个处理异常的语句的时候others必须要写在最后面。

#####   异常关键字：

##### 				SQLCODE:获取异常编码

##### 				SQLERRM:获取异常的信息

######   建议：实际的工作中，所有的异常基本都使用others进行捕获，并且在异常处理的时候讲异常编码和异常信息保存到日志表中。



## 	2.2、异常分类：

#####    	2.2.1.系统预定义异常：

​    	 数据库中已经定义好的异常，直接可以拿过来使用，一般Oracle数据库中预定义好的异常有二十多个。
​     	常见的预定义异常：

######      		zero_divide:除数为0异常

######      		no_data_found:当执行select...into语句的时候没有查询到数据。

######      		too_many_rows:当执行select...into语句的时候查询到多行数据。

######     		 cursor_alread_open:试图打开一个已经被打开的游标。

######      		invalid_cursor:代表一个非法的游标操作，试图关闭一个没有打开的游标。

######      		others:可以用来捕获所有的异常。



#####   --异常处理

```plsql
  EXCEPTION
    WHEN zero_divide THEN
      dbms_output.put_line('除数不能为0');
    WHEN no_data_found THEN
       dbms_output.put_line('没有查询到数据');
    WHEN OTHERS THEN
      dbms_output.put_line('程序出现了问题！');
      --获取异常编码：
      SQL_code:=SQLCODE;
      --获取异常的信息：
      sql_error:=SQLERRM;
      dbms_output.put_line('异常编码：'||sql_code||',异常信息：'||sql_error);
 END;  
```



##### --计算两个数字的商

```plsql
DECLARE
  a INT :=&a;
  b INT :=&b;
  c INT;
  v_name emp.ename%TYPE;
  sql_code VARCHAR2(20);  --保存异常编码
  sql_error VARCHAR2(200);--保存异常信息
  BEGIN
    c:=a/b;
    dbms_output.put_line(c);
    
    SELECT ename INTO v_name FROM emp ;
```



#####    	2.2.2.用户自定义异常：

######       		2.2.2.1.错误编号异常：

​            将数据库中出现的一些异常编号，和一个异常变量名称进行绑定，在后续处理该类异常的时候可以直接使用异常变量名。
​            步骤：

##### 			2.2.2.1.1.定义一个异常变量名 ;

​                   变量名  exception;

#####             2.2.2.1.2.将异常变量名和错误编号进行绑定

​                    pragma exception_init(异常变量名,错误编号);

#####            2.2.2.1.3.在进行捕获异常的时候可以使用异常变量名来进行处理

######             注意：错误编号异常，其实也算是系统异常，因为异常的引发是由系统进行引发，并不会由用户去引发。



######       		2.2.2.2.业务逻辑异常：用户可以根据自己的业务逻辑的需要来定义一个异常，并且在规定的时间或者是场景下面去引发。

​          步骤：

##### 			2.2.2.2.1.定义一个异常变量

​               变量名  exception;

#####             2.2.2.2.2.在合适的时间对异常进行引发

​                 raise  异常名称

#####             2.2.2.2.3.使用exception来处理异常

######    注意：在PL/SQL块中如果是有引发异常，则一般建议必须要使用exception对异常进行处理。



### --自定义一个异常，当添加部门信息的时候如果部门名称为null则引发异常。

```plsql
declare
--定义一个异常变量
 dname_null EXCEPTION;
 --定义变量
 row_dept dept%ROWTYPE;
 BEGIN
  --赋值
  row_dept.deptno:=67;
  row_dept.dname:='zzz';
  row_dept.loc:='深圳';
  --添加数据
  INSERT INTO dept VALUES(row_dept.deptno,row_dept.dname,row_dept.loc);
  --判断部门名称是否为null,为空则引发异常
  IF row_dept.dname IS NULL THEN
    --引发异常
    RAISE  dname_null;
  END IF;
  dbms_output.put_line('添加成功！'); 
  --提交事务
  COMMIT;
 --异常处理
EXCEPTION
  WHEN dname_null THEN
    dbms_output.put_line('部门名称不能为空！'); 
     --回滚事务
    ROLLBACK;
END;
```



### --编写一个PL/SQL块，定义一个错误编号异常，用来绑定-00001编号异常

```plsql
DECLARE
 --定义一个异常变量名
 primary_exp EXCEPTION;
 --绑定错误编号和异常变量
 PRAGMA  Exception_Init(primary_exp,-00001);
BEGIN
  INSERT INTO dept VALUES(10,'','');
  EXCEPTION
    WHEN primary_exp THEN
      dbms_output.put_line('主键不能重复！');
END;
```

