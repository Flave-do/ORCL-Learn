#  1.程序包：

###### 就是将多个相关的子程序（函数,存储过程，类型等）封装到一起，作为一个整体保存到数据库中。

#### 作用：

##### 	1.提高整体的程序性能

##### 	2.方便管理子程序。

#### 一个完整的程序包是由两部分组成：

#####      1.程序包规范：package

​       	主要是用来规定程序包中应该存放的一些子程序或者其他的类型等等的声明。

##### 创建一个程序包规范的语法：

```plsql
       create [or replace] package 包名
       is
       [游标声明]
       [类型声明]
       [变量声明]
       [函数声明]
       [存储过程声明]
       .....
       end [包名]； 
```

######           注意：程序包的规范不能直接使用，必须要为他创建主体。



#  2.程序包主体：package body

######  主要就是对程序包规范中规定的子程序进行具体的实现。

####  创建的语法：

```plsql
   create or replace package body 包名
   is
   [各种子程序的实现]
   end [包名];
```

  

####   程序包中的子程序的调用：

```plsql
程序包名.子程序名称[参数列表];
```



#### --编写一个程序包：里面包含一个函数：计算某一个部门的平均工资

#### -- 包含了一个存储过程：查询指定员工的姓名和工资

```plsql
CREATE OR REPLACE  PACKAGE pack_emp 
IS
--声明函数
  FUNCTION fun_avg_sal(d_no NUMBER) RETURN NUMBER;
--声明存储过程
  PROCEDURE pro_emp(e_no IN NUMBER,e_name OUT VARCHAR2,e_sal OUT NUMBER);
END pack_emp;


--编写程序包主体
CREATE OR REPLACE PACKAGE BODY pack_emp
IS
--实现函数
   FUNCTION fun_avg_sal(d_no NUMBER) RETURN NUMBER
     IS
     avg_sal NUMBER;
   BEGIN
     SELECT AVG(sal) INTO avg_sal FROM emp WHERE deptno=d_no;  
     --返回平均工资
     RETURN round(avg_sal,2);
   END fun_avg_sal;

--实现存储过程
   PROCEDURE pro_emp(e_no IN NUMBER,e_name OUT VARCHAR2,e_sal OUT NUMBER)
     IS
     BEGIN
       SELECT ename,sal INTO e_name,e_sal FROM emp WHERE empno=e_no;
     END pro_emp;
 END pack_emp;


--调用：
SELECT pack_emp.fun_avg_sal(30) FROM dual;

--调用存储过程
DECLARE
 ename VARCHAR2(20);
 sal NUMBER;
 BEGIN
   pack_emp.pro_emp(7369,ename,sal);
   dbms_output.put_line(ename||','||sal);
 END;
```



# REF引用游标：

######    作用：主要是用来从存储过程中返回一个结果集。

​      使用步骤：

#####         1.定义一个引用游标的数据类型。

​            type 类型名 is ref cursor;

#####         2.可以在存储过程中使用该引用游标类型来返回一个结果集。

​           参数名  out  包名.引用游标类型名;

#####         3.在进行实现存储过程的时候，需要将sql语句执行的结果放入到游标输出参数中。

​           open 游标参数名  for  select 语句;

##### 		4.在调用有游标类型的存储过程的时候，需要定义游标类型的变量来接受数据。

######       注意：在操作游标的时候不能使用for循环，也不能再open游标，但是可以使用loop或者while循环来操作。



#### --编写一个程序包，里面包含了一个存储过程：查询员工表中指定部门的员工信息，将员工信息进行返回

#### --定义程序包规范

```plsql
CREATE OR REPLACE PACKAGE pack_e 
IS
--声明一个引用游标的类型
TYPE cur_emp IS REF CURSOR;
--声明一个存储过程
PROCEDURE pro_emp_info(d_no NUMBER,emp_info OUT pack_e.cur_emp);
END pack_e;


--编写程序包主体
CREATE OR REPLACE PACKAGE BODY pack_e
IS

PROCEDURE pro_emp_info(d_no NUMBER,emp_info OUT pack_e.cur_emp)
  IS
  BEGIN
    --将sql语句查询出来的结果保存到emp_info游标参数中。
    OPEN emp_info FOR SELECT * FROM emp WHERE deptno=d_no;
  END pro_emp_info;
 END pack_e;

--调用有输出游标的参数的存储过程
DECLARE
 --定义一个游标类型的变量
 row_emp pack_e.cur_emp;
 --定义变量
 r_e emp%ROWTYPE;
 BEGIN
   pack_e.pro_emp_info(30,row_emp);
   --操作游标
   LOOP
     --读取游标
     FETCH row_emp INTO r_e;
     EXIT WHEN row_emp%NOTFOUND;
     dbms_output.put_line(r_e.ename||','||r_e.sal);
     END LOOP;
    --关闭游标 
    CLOSE row_emp;
END ;
```